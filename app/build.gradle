//apply plugin: 'com.android.application'

apply plugin: 'com.android.library'

apply plugin: 'maven'
// 使用signing plugin做数字签名
apply plugin: 'signing'
//定义GroupID和Version，ArtifactID会自动使用Project名
group = 'com.github.xiaoliang0227'
archivesBaseName = "TriangleView"
version = '1.0.1'

repositories {
    mavenCentral()
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            // 为Pom文件做数字签名
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            // 指定项目部署到的中央库地址
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName:ossrhUsername, password:ossrhPassword)
            }
            snapshotRepository(url:'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName:ossrhUsername, password:ossrhPassword)
            }

            // 构造项目的pom文件
            pom.project {
                name project.name
                packaging 'aar'
                description '三角角标控件'
                url 'https://github.com/xiaoliang0227/TriangleView'

                scm {
                    url 'scm:git@github.com:xiaoliang0227/TriangleView.git'
                    connection 'scm:git@github.com:xiaoliang0227/TriangleView.git'
                    developerConnection 'git@github.com:xiaoliang0227/TriangleView.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'Jason Zhao（赵永亮）'
                    }
                }

                developers {
                    developer {
                        id 'yongliang'
                        name 'Jason Zhao（赵永亮）'
                        email '503518700@qq.com'
                    }
                }
            }
        }
    }
}


android {
    compileSdkVersion 26
    defaultConfig {
//        applicationId "com.zyl.triangleview"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
